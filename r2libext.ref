* Copyright 2024 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2024-05-24
* https://github.com/Aleksandr3Bocharov/r2libext

*====================================================================
* Расширение библиотеки Рефал-2.
* http://www.refal.net/~belous/refal2-r.htm
*====================================================================

R2LIBEXT    START
            ENTRY LoadFile, SaveFile
            ENTRY Trim, Trim-L, Trim-R, WhiteSpaces 
            ENTRY Exist, Set
            EMPTY True, False

* <LoadFile SN EF> == (EL)*
LoadFile +
    SN EF   = <Open 'r' SN EF> +
            <DoLoadFile SN <Get SN>> +
            <Close 'r' SN>
  
DoLoadFile +
    SN 0    =
    SN EL 0 = (EL)
    SN EL   = (EL) <DoLoadFile SN <Get SN>>
  
* <Trim E> == E
Trim +
    EL      = <Trim-L <Trim-R EL>>

WhiteSpaces S   ' \n\t\v\f\r'

* <Trim-L E> == E
Trim-L +
    S(:WhiteSpaces:)L EL +
            = <Trim-L EL>
    EL      = EL
  
* <Trim-R E> == E
Trim-R +
    EL S(:WhiteSpaces:)L +
            = <Trim-R EL>
    EL      = EL
  
* <Exist W E> == /True/ | /False/
Exist +
    WS EB WS EE +
            = /True/
    WS EI   = /False/
  
* <Set W*> == W*
Set +
    EU WR EB WR EE +
            = EU <Set EB WR EE>
    EU      = EU

            END