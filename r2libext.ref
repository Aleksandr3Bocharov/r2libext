* Copyright 2024 Aleksandr Bocharov
* Distributed under the Boost Software License, Version 1.0.
* See accompanying file LICENSE_1_0.txt
* or copy at http://www.boost.org/LICENSE_1_0.txt
* 2024-05-31
* https://github.com/Aleksandr3Bocharov/r2libext

*====================================================================
* Расширение библиотеки Рефал-2.
* http://www.refal.net/~belous/refal2-r.htm
*====================================================================

R2LIBEXT    START
            EXTRN XOpen, XClose, XGet
            ENTRY Call
            ENTRY LoadFile
            ENTRY Trim, TrimL, TrimR, WhiteSpaces(WSpaces) 
            ENTRY Exist, Set
            EMPTY True, False

* <Call WC EA> == ER
Call +
    SF EA   = < SF EA>
    (WC EB) EA +
            = <Call WC EB EA>

* <LoadFile SN EF> == (EL)*
LoadFile +
    SN EF   = <XOpen 'r' SN EF> +
            <DoLoadFile SN <XGet SN>> +
            <XClose 'r' SN>
  
DoLoadFile +
    SN /0/  =
    SN EL /0/ +
            = (EL)
    SN EL   = (EL) <DoLoadFile SN <XGet SN>>
  
* <Trim E> == E
Trim +
    EL      = <TrimL <TrimR EL>>

WhiteSpaces S   ' \n\t\v\f\r'

* <TrimL E> == E
TrimL +
    S(:WhiteSpaces:)W EL +
            = <TrimL EL>
    EL      = EL
  
* <TrimR E> == E
TrimR +
    EL S(:WhiteSpaces:)W +
            = <TrimR EL>
    EL      = EL
  
* <Exist W E> == /True/ | /False/
Exist +
    WS EB WS EE +
            = /True/
    WS EI   = /False/
  
* <Set W*> == W*
Set +
    EU WR EB WR EE +
            = EU <Set EB WR EE>
    EU      = EU

            END